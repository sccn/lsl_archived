apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "edu.ucsd.sccn.receivestringmarkers"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions -Wno-redeclared-class-member -Wno-deprecated-declarations -Wno-expansion-to-defined -Wno-deprecated-register"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "../CMakeLists.txt"
        }
    }

    dexOptions {
        javaMaxHeapSize "2048m"
    }

    sourceSets {
        main {
            java.srcDirs += '../../../liblsl-Java/src'
        }
    }
}

dependencies {
    implementation fileTree(dir: '../libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:24.2.1'
    testImplementation 'junit:junit:4.12'
}

//copy native libraries to per project location
task copyJniLibs(type: Copy) {
    from '../jniLibs'
    into 'src/main/jniLibs'
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(copyJniLibs)
}
clean.dependsOn 'cleanCopyJniLibs'

